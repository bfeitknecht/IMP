Letter = 'a' .. 'z' | 'A' .. 'Z'
Digit = '0' .. '9'
Ident = Letter { Letter | Digit }*
Numeral = Digit | Numeral Digit
Var = Ident

Op = '+' | '-' | '*'                                                                -- arithmetic operation
Aexp
    = '(' Aexp Op Aexp ')'                                                          -- computation
    | Var                                                                           -- variable
    | Numeral                                                                       -- value

Rop = '=' | '#' | '<' | '<=' | '>' | '>='                                           -- relation operation
Bexp
    = '(' Bexp 'or' Bexp ')'                                                         -- disjunction
    | '(' Bexp 'and' Bexp ')'                                                        -- conjunction
    | 'not' Bexp                                                                    -- negation
    | Aexp Rop Aexp                                                                 -- relation

Stm
    = 'skip'                                                                        -- no-op
    | Var ':=' Aexp                                                                 -- definition
    | '(' Stm ';' Stm ')'                                                           -- sequence
    | 'if' Bexp 'then' Stm 'else' Stm 'end'                                         -- conditional
    | 'while' Bexp 'do' Stm 'end                                                    -- iteration
    | Ext                                                                           -- extension

Ext
    = 'var' Var ':=' Aexp 'in' Stm 'end'                                            -- local variable definition
    | 'procedure' Ident '(' { Var ',' }* '~' { Var ',' }* ')' 'begin' Stm 'end'    -- procedure definition
    | Ident '(' { Aexp ',' }* '~' { Var ',' }* ')' 'begin' Stm 'end'               -- procedure invocation
    | Stm '||' Stm                                                                  -- non-determinism
    | Stm 'par' Stm                                                                 -- parallelization
